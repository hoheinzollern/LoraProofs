
(* I start by defining some constants that I suspect might by of significance, or maybe I just use them for certain values
to not be exactly the same  *)
const K_2m_constantstring : bitstring.
const IV_2m_constantstring : bitstring.
const K_3m_constantstring : bitstring.
const IV_3m_constantstring : bitstring.
const K_3ae_constantstring : bitstring.
const IV_3ae_constantstring : bitstring.
const K_4ae_constantstring : bitstring.
const IV_4ae_constantstring : bitstring.
const SCK_constantstring : bitstring.
const RCK_constantstring : bitstring.
const emptystring : bitstring.

const cred_AS : bitstring. (* possible point of contention *)
const cred_ED : bitstring.




(* hashinh primitive: *)

fun sha256(bitstring): bitstring.

(* key derivation functions *)
type extractkey.
type expandkey.
const emptyexpandKey : expandkey.
const emptyextractKey : extractkey.


(* diffie hellman ratchet shceme *)

type G. (*group elements *)
const g: G [data].
type exponent. (*exponent used for creating pk from secret key *)
fun exp(G,exponent): G.
equation forall x:exponent, y:exponent; exp(exp(g,x),y) = exp(exp(g,y), x).





(* key ratchet shceme, including encryption and decryption *)
type ratchKey.
fun ratchetKey(G, bitstring): ratchKey. (* grrrr why won't you return two things????! *)
fun ratchetSeed(G,bitstring):bitstring.
fun senc ( bitstring , ratchKey ) : bitstring.
reduc forall m: bitstring , k : ratchKey ; sdec(senc(m, k) , k ) = m.

(* extract functions: *)



fun HKDFextract(extractkey, G) : extractkey.


fun HKDFexpand(extractkey,  bitstring, bitstring) : expandkey. (*input: expandkey, hash, constant string *)

(* authenticated encryption *)

(* argument of the form: Key, IV , plaintext, associated auth data *)
fun AEAD_enc(expandkey,expandkey, bitstring, bitstring) : bitstring.

reduc forall k:expandkey, IV:expandkey, p:bitstring, AD:bitstring;
    AEAD_dec(AEAD_enc(k,IV,p,AD), k, IV, AD) = p.

reduc forall k:expandkey, IV:expandkey, p:bitstring, AD:bitstring; (* for the case that an empty string is used for plaintext *)
    AEAD_dec_unsafe(AEAD_enc(k,IV,p,AD), k, IV) = p.

fun epxandKey_bitstring(expandkey) : bitstring [typeConverter].

(* XOR *)


fun xor(bitstring,bitstring): bitstring.

reduc forall a:bitstring, b:bitstring;
    xordecrypt(xor(a,b), b) = a. (* for advsersary *)
reduc forall a:bitstring, b:bitstring;
    xorBreak(xor(a,b), a) = b.  (* for advsersary *)

(*))AppKey :key, NwkKey :key, JoinEUI :bitstring,DevEUI :bitstring, DevNonce : bitstring) = *)

(*TODO: increment counter nonce *)
event debugED().
event debugAS().

event EDreceive(bitstring).
event EDsend(bitstring).
event ASreceive(bitstring).
event ASsend(bitstring).

event gotSckED(ratchKey).
event gotSckAS(ratchKey).

 

query m : bitstring;
    event (ASsend(m))==> event (EDreceive(m)).



query m : bitstring;
    event (EDsend(m))==> event (ASreceive(m)).




event sendmsg(bitstring).
event rcvmsg(bitstring).




free c :channel.
free downlink : channel.
free uplink : channel.

free store_uplink : channel [private].

let ED(devEUI : bitstring, appEUI : bitstring) = 
    (* generate key pair *)

    new privEK_ED :exponent;
    let pubEK_ED = exp(g, privEK_ED) in

    (* sending message 1 *)
    out(c, (pubEK_ED, devEUI, appEUI));

    let intermediateHash = sha256((pubEK_ED, devEUI, appEUI)) in (* calculating the hash on m1 *)
    (* receiving message 2: *)
    in(c, (pubEK_AS : G, =appEUI, ciphertext_2 : bitstring)); 
    let PRK_2e = HKDFextract(emptyextractKey, exp(pubEK_AS, privEK_ED)) in
    let TH_2 = sha256((intermediateHash, pubEK_AS, appEUI)) in 
    let Keystream_2 = epxandKey_bitstring(HKDFexpand(PRK_2e, TH_2, emptystring)) in
    let (=cred_AS, MAC_2 : bitstring) = xorBreak(ciphertext_2, Keystream_2) in (* this is weird, I can put anything in cred_AS *)
    let PRK_3e2m = HKDFextract(PRK_2e,exp(pubEK_AS, privEK_ED)) in
    let  K_2m = HKDFexpand(PRK_3e2m, TH_2,K_2m_constantstring) in 
    let IV_2m = HKDFexpand(PRK_3e2m, TH_2, IV_2m_constantstring) in
    let A = (TH_2, cred_AS) in 
    if AEAD_enc(K_2m, IV_2m, emptystring, A) = MAC_2 then

    (* generating message 3 *)

    let TH_3 = sha256((TH_2, ciphertext_2)) in 
    let PRK_4x3m = HKDFextract(PRK_3e2m, exp(pubEK_AS, privEK_ED)) in
    let  K_3m = HKDFexpand(PRK_4x3m, TH_3,K_3m_constantstring) in 
    let IV_3m = HKDFexpand(PRK_3e2m, TH_3, IV_3m_constantstring) in
    let A1 = (TH_2, cred_AS) in 
    let MAC_3 = AEAD_enc(K_3m, IV_3m, emptystring, A) in
    let K_3ae = HKDFexpand(PRK_3e2m, TH_3,K_2m_constantstring) in 
    let IV_3ae = HKDFexpand(PRK_3e2m, TH_3, IV_3ae_constantstring) in
    let P_2 = (cred_ED, MAC_3) in
    let A_1 = (emptystring, TH_3) in 
    let ciphertext_3 = AEAD_enc(K_3ae, IV_3ae, P_2, A_1) in
    out(c, ciphertext_3);
    (*receving message 4 *)
    in(c,ciphertext_4: bitstring);
    let TH_4 = sha256((TH_3, ciphertext_3)) in
    let A_1_tmp = (emptystring, TH_3) in 
    let K_4ae = HKDFexpand(PRK_4x3m,TH_4,K_4ae_constantstring) in
    let IV_4ae = HKDFexpand(PRK_4x3m,TH_4,IV_4ae_constantstring) in
    let (=emptystring) = AEAD_dec(ciphertext_4,K_4ae, IV_4ae, A_1) in

    (* receiving racthet seed: *)
    let downlinkRatchet_Seed0 =  epxandKey_bitstring(HKDFexpand(PRK_4x3m, TH_4, RCK_constantstring)) in
    (* sending racthet seed: *)
    let uplinkRatchet_Seed0 = epxandKey_bitstring(HKDFexpand(PRK_4x3m, TH_4, SCK_constantstring)) in

    let DHsharekey_0 = exp(pubEK_AS,privEK_ED ) in (* constructing the first DH shared key from already existing DH keys *)

    (* downlink keys*)
    let mkSK_0_down = ratchetKey(DHsharekey_0, downlinkRatchet_Seed0) in
    let seed_0_down = ratchetSeed(DHsharekey_0, downlinkRatchet_Seed0) in
    (* uplink keys*)

    let mkSK_0_up = ratchetKey(DHsharekey_0, uplinkRatchet_Seed0) in
    let seed_0_up = ratchetSeed(DHsharekey_0, uplinkRatchet_Seed0) in
    new message_0: bitstring;
    let c_1_uplink = senc(message_0, mkSK_0_down) in
    new store_downlink_ED : channel;
    new store_uplink_ED : channel;

    out(store_uplink_ED, (mkSK_0_up,seed_0_up));
    out(store_downlink_ED, (mkSK_0_down, seed_0_down)); (* simply doing this for being able to get the things sent out again *)


    (out(uplink, c_1_uplink) |
    (
        in(store_downlink_ED,  (sharekey_in_down :ratchKey, seed_in_down:bitstring)) [precise];
        in(downlink, c_1_down:bitstring) [precise];
        let message_0_down = sdec(c_1_down,sharekey_in_down) in
        event EDreceive(message_0_down);
        let mkSK_i_down = ratchetKey(DHsharekey_0, seed_in_down) in
        let seed_i_down = ratchetSeed(DHsharekey_0, seed_in_down) in
        out(store_downlink_ED, (mkSK_i_down, seed_i_down));


        in(store_uplink_ED, (mkSK_in_up:ratchKey, seed_in_up:bitstring)) [precise];

        let mkSK_i_up = ratchetKey(DHsharekey_0, seed_in_up) in
        let seed_i_up = ratchetSeed(DHsharekey_0, seed_in_up) in
        new message_i_up : bitstring;
        event EDsend(message_i_up);
        out(store_downlink_ED, (mkSK_i_up, seed_0_up));
        out(uplink, senc(message_i_up,mkSK_i_up)))).






let AS(devEUI : bitstring, appEUI : bitstring) = 
    (* generate key pair *)
    new privEK_AS :exponent;
    let pubEK_AS = exp(g,privEK_AS) in
    (* getting message one and unpack: *)
    in(c,m1:bitstring);
    let (pubEK_ED : G, =devEUI, =appEUI ) = m1 in

    (* generating message 2: *)
    let intermediateHash = sha256(m1) in 
    let TH_2 = sha256((intermediateHash, pubEK_AS, appEUI)) in 
    let PRK_2e = HKDFextract(emptyextractKey, exp(pubEK_ED, privEK_AS)) in
    let PRK_3e2m = HKDFextract(PRK_2e,exp(pubEK_ED, privEK_AS)) in
    let  K_2m = HKDFexpand(PRK_3e2m, TH_2,K_2m_constantstring) in 
    let IV_2m = HKDFexpand(PRK_3e2m, TH_2, IV_2m_constantstring) in 
    let A_0 = (TH_2, cred_AS) in 
    let MAC_2 = AEAD_enc(K_2m, IV_2m, emptystring, A_0) in
    let P_2 = (cred_AS, MAC_2) in
    let Keystream_2 = HKDFexpand(PRK_2e, TH_2,emptystring) in
    let ciphertext_2 = xor(P_2, epxandKey_bitstring(Keystream_2)) in
    out(c, (pubEK_AS, appEUI, ciphertext_2)); 

    (* receiving message 3 *)
    in(c, ciphertext_3 : bitstring);
    let TH_3 = sha256((TH_2, ciphertext_2)) in 
    let PRK_4x3m = HKDFextract(PRK_3e2m, exp(pubEK_ED, privEK_AS)) in
    let  K_3m = HKDFexpand(PRK_4x3m, TH_3,K_3m_constantstring) in 
    let IV_3m = HKDFexpand(PRK_3e2m, TH_3, IV_3m_constantstring) in
    let K_3ae = HKDFexpand(PRK_3e2m, TH_3,K_3ae_constantstring) in
    let IV_3ae = HKDFexpand(PRK_3e2m, TH_3, IV_3ae_constantstring) in
    let A_1 = (emptystring, TH_3) in 
    let (=cred_ED, MAC_3 : bitstring) = AEAD_dec(ciphertext_3,K_3ae, IV_3ae, A_1) in (* I can put in any value Ã¥for ID_CRED??? *)
    if AEAD_enc(K_3m, IV_3m, emptystring, A_1) = MAC_3 then



    (*generating message 4*)
    let TH_4 = sha256((TH_3, ciphertext_3)) in
    let K_4ae = HKDFexpand(PRK_4x3m,TH_4,K_4ae_constantstring) in
    let IV_4ae = HKDFexpand(PRK_4x3m,TH_4,IV_4ae_constantstring) in
    let A_2 = (emptystring, TH_3) in 
    let ciphertext_4 = AEAD_enc(K_4ae, IV_4ae, emptystring, A_2) in
    out(c, ciphertext_4);


     (* receiving racthet seed: *)
    let downlinkRatchet_Seed0 =  epxandKey_bitstring(HKDFexpand(PRK_4x3m, TH_4, RCK_constantstring)) in
    (* sending racthet seed: *)
    let uplinkRatchet_Seed0 = epxandKey_bitstring(HKDFexpand(PRK_4x3m, TH_4, SCK_constantstring)) in
    (* receving public DH ratchet public key, gener ating DH ratchet keys *)

    let DHsharekey_0 = exp(pubEK_ED,privEK_AS ) in (* constructing the first DH shared key from already existing DH keys *)
    event debugAS();


    let mkSK_0_down = ratchetKey(DHsharekey_0, downlinkRatchet_Seed0) in
    let seed_0_down = ratchetSeed(DHsharekey_0, downlinkRatchet_Seed0) in
    let mkSK_0_up = ratchetKey(DHsharekey_0, uplinkRatchet_Seed0) in
    let seed_0_up = ratchetSeed(DHsharekey_0, uplinkRatchet_Seed0) in
    new message_0 : bitstring;
    let c_1 = senc(message_0, mkSK_0_down) in

    new store_downlink_AS : channel;
    new store_uplink_AS : channel;

    out(store_downlink_AS, (mkSK_0_down, seed_0_down)); (* simply doing this for being able to get the things sent out again *)
    out(store_uplink_AS, (mkSK_0_up, seed_0_up));


    (out(downlink, c_1) |
    (
        in(store_downlink_AS,( mkSK_i_storedown: ratchKey, seed_storedown :bitstring)) [precise];

        (* ratchet *)
        let mkSK_i_down = ratchetKey(DHsharekey_0, seed_storedown) in
        let seed_i_SCKdown = ratchetSeed(DHsharekey_0, seed_storedown) in
        new message_i : bitstring;
        event ASsend(message_i);
        (*send next message and send to own channel  *)
        out(downlink, (mkSK_i_down, senc(message_i, mkSK_i_down)));
        out(store_downlink_AS, (mkSK_i_down, seed_i_SCKdown));
        
        in(uplink, C_i_up:bitstring);
        in(store_uplink_AS, (mkSK_i_storeup: ratchKey, seed_storeup:bitstring));
        let message_i_up = sdec(C_i_up,mkSK_i_storeup) in
        event ASreceive(message_i_up);

        let mkSK_i_up = ratchetKey(DHsharekey_0, seed_storeup) in
        let seed_i_up = ratchetSeed(DHsharekey_0, seed_storeup) in
        out(store_downlink_AS, (mkSK_i_up,seed_i_up))
        )).





process
    new devEUI :bitstring;
    new appEUI :bitstring;
    ((ED(devEUI, appEUI)) | (AS(devEUI, appEUI)))